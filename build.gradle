apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

version = '0.8.1'
group = 'org.irmacard.idemix'

// Our directory structure differs from the default
sourceSets {
    main.java.srcDir 'src'
    test.java.srcDir 'tests'
}

repositories {
    mavenLocal()
    maven {
        url "https://credentials.github.io/repos/maven2/"
    }
    mavenCentral()
}

dependencies {
    compile "org.irmacard.credentials:credentials_idemix:0.8.1"
    compile "net.sourceforge.scuba:scuba_smartcards:0.0.3"
    compile 'com.google.code.gson:gson:2.2.2'

    testCompile files('lib/cert-cvc-1.3.0.jar')
    testCompile "junit:junit:4.11"
    testCompile "net.sourceforge.scuba:scuba_sc_j2se:0.0.3"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

if ( project.hasProperty("mavenRepositoryIRMA") ) {
    uploadArchives.repositories.mavenDeployer {
        repository(url: mavenRepositoryIRMA)
    }
}

/*
 * Expose a subset of the tests for easy issuance/verification/delation of
 * credentials. Tests are not run by default during a build, only if they are
 * explicitly called for.
 */
test {
    include "**/TestIRMACredential*"
}
test.onlyIf { project.gradle.startParameter.taskNames.contains("test") }
test.dependsOn cleanTest // Make sure it is always run

tasks.withType(Test) {
    testLogging {
        events 'passed'
        if ( project.hasProperty("verbose") ) {
            showStandardStreams = true
        }
    }
}
