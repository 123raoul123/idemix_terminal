apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

version = '0.11.2'
group = 'org.irmacard.idemix'

repositories {
    mavenLocal()
    maven {
        url "https://credentials.github.io/repos/maven2/"
    }
    mavenCentral()
}

dependencies {
    compile "org.irmacard.credentials:credentials_idemix:0.11.1"
    compile "net.sf.scuba:scuba_smartcards:0.0.7-irma"
    compile 'com.google.code.gson:gson:2.2.2'
    compile 'org.slf4j:slf4j-api:1.7.12'

    testCompile files('lib/cert-cvc-1.3.0.jar')
    testCompile "junit:junit:4.11"
    testCompile "net.sf.scuba:scuba_sc_j2se:0.0.7-irma"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

if ( project.hasProperty("mavenRepositoryIRMA") ) {
    uploadArchives.repositories.mavenDeployer {
        repository(url: mavenRepositoryIRMA)
    }
}

/*
 * Expose a subset of the tests for easy issuance/verification/delation of
 * credentials. Tests are not run by default during a build, only if they are
 * explicitly called for.
 */
test {
    include "**/TestIRMACredential*"
    include "**/TestCard*"
}
test.onlyIf { project.gradle.startParameter.taskNames.contains("test") }
test.dependsOn cleanTest // Make sure it is always run

tasks.withType(Test) {
    testLogging {
        events 'passed'
        if ( project.hasProperty("verbose") ) {
            showStandardStreams = true
        }
    }
}
